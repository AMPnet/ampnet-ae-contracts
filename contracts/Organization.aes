contract CoopInterface = 
    entrypoint is_wallet_active : (address) => bool

contract Organization =

    datatype event =
        OrganizationCreated

    record state = {
        coop       : CoopInterface,
        owner      : address,
        members    : map(address, bool) }
    
    stateful entrypoint init(coop: CoopInterface) : state =
        require(
            coop.is_wallet_active(Call.origin),
            "Cannot create Organization, caller not registered Cooperative user.")
        Chain.event(OrganizationCreated)
        { coop = coop,
          owner = Call.origin, 
          members = {} }
    
    stateful entrypoint add_member(member: address) =
        only_owner()
        only_active_organization()
        only_active_wallet(member)
        put(state{ members[member] = false })
    
    stateful entrypoint confirm_membership() =
        only_active_organization()
        only_active_wallet(Call.origin)

        let approval = Map.lookup(Call.origin, state.members)
        switch(approval)
            None => abort("User tried to accept Organization invite which does not exist!")
            Some(membershipConfirmed) =>
                if (membershipConfirmed)
                    abort("User tried to accept Organization invite but it is already accepted!")
                else
                    put(state{ members[Call.origin] = true })
    
    entrypoint owner() : address = state.owner

    entrypoint coop() : CoopInterface = state.coop

    entrypoint is_verified() : bool = state.coop.is_wallet_active(Contract.address)

    function only_owner() =
        require(Call.origin == state.owner, "Only organization owner can make this action!")

    function only_active_wallet(wallet: address) =
        require(
            state.coop.is_wallet_active(wallet),
            "Only registered Cooperative member can make this action.")
    
    function only_active_organization() =
        require(
            state.coop.is_wallet_active(Contract.address),
            "Organization must have active wallet!")