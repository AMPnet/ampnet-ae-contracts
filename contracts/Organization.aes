contract CoopInterface = 
    function is_wallet_active : (address) => bool

contract Organization =
    record state = {
        coop       : CoopInterface,
        owner      : address,
        members    : map(address, bool) }
    
    public stateful function init(coop: CoopInterface) : state =
        require(
            coop.is_wallet_active(Call.caller),
            "Cannot create Organization, caller not registered Cooperative user.")
        { coop = coop,
          owner = Call.caller, 
          members = {} }
    
    public stateful function add_member(member: address) =
        only_owner()
        only_active_organization()
        only_active_wallet(member)
        put(state{ members[member] = false })
    
    public stateful function confirm_membership() =
        only_active_organization()
        only_active_wallet(Call.caller)

        let approval = Map.lookup(Call.caller, state.members)
        switch(approval)
            None => abort("User tried to accept Organization invite which does not exist!")
            Some(membershipConfirmed) =>
                if (membershipConfirmed)
                    abort("User tried to accept Organization invite but it is already accepted!")
                else
                    put(state{ members[Call.caller] = true })
    
    public function owner() : address = state.owner

    public function coop() : CoopInterface = state.coop

    public function is_verified() : bool = state.coop.is_wallet_active(Contract.address)

    private function require(b : bool, err : string) =
        if(!b) 
            abort(err)
    
    private function only_owner() =
        require(Call.caller == state.owner, "Only organization owner can make this action!")

    private function only_active_wallet(wallet: address) =
        require(
            state.coop.is_wallet_active(wallet),
            "Only registered Cooperative member can make this action.")
    
    private function only_active_organization() =
        require(
            state.coop.is_wallet_active(Contract.address),
            "Organization must have active wallet!")