contract TokenInterface =
    entrypoint balance_of : (address) => (int)

contract Coop =
    record state = {
        owner                      : address,
        activeWallets              : map(address, bool),
        token                      : option(TokenInterface) }
    
    stateful entrypoint init() : state =
        { owner = Call.caller,
          activeWallets = {},
          token = None }
    
    stateful entrypoint set_token(token: TokenInterface) =
        only_owner()
        put(state{ token = Some(token) })

    stateful entrypoint add_wallet(wallet: address) =
        only_owner()
        put(state{ activeWallets[wallet] = true })

    stateful entrypoint add_wallets(wallets: list(address)) =
        only_owner()
        recursive_wallets_add(wallets)

    entrypoint is_wallet_active(wallet: address) : bool = state.activeWallets[wallet]

    entrypoint owner() : address = state.owner
    
    entrypoint timestamp() : int = Chain.timestamp

    entrypoint token() : TokenInterface = 
        switch (state.token)
            None => abort("Token not initalized!")
            Some(token) => token

    stateful function recursive_wallets_add(wallets: list(address)) : int =
        switch (wallets)
            wallet::wallets' =>
                put(state{ activeWallets[wallet] = true })
                1 + recursive_wallets_add(wallets')
            [] => 0

    function only_owner() =
        require(Call.caller == state.owner, "Only owner can make this action!")
