contract TokenInterface =
    entrypoint balance_of : (address) => (int)

contract Coop =

    datatype event =
        WalletAdded(address)
      | CoopOwnershipChanged(address)

    record state = {
        owner                      : address,
        activeWallets              : map(address, bool),
        token                      : option(TokenInterface),
        claimOwnershipCalled       : bool }
    
    stateful entrypoint init() : state =
        { owner = Call.origin,
          activeWallets = {},
          token = None,
          claimOwnershipCalled = false }
    
    stateful entrypoint claim_ownership(newOwner: address) =
        require(!state.claimOwnershipCalled, "#Error: trying to claim ownership more than once. Aborting.#")
        put(state{
            owner = newOwner,
            claimOwnershipCalled = true })
        Chain.event(CoopOwnershipChanged(newOwner))
    
    stateful entrypoint transfer_ownership(newOwner: address) =
        only_owner()
        put(state{ owner = newOwner })
        Chain.event(CoopOwnershipChanged(newOwner))
    
    stateful entrypoint set_token(token: TokenInterface) =
        only_owner()
        put(state{ token = Some(token) })

    stateful entrypoint add_wallet(wallet: address) =
        only_owner()
        put(state{ activeWallets[wallet] = true })
        Chain.event(WalletAdded(wallet))

    stateful entrypoint add_wallets(wallets: list(address)) =
        only_owner()
        recursive_wallets_add(wallets)

    entrypoint is_wallet_active(wallet: address) : bool = state.activeWallets[wallet = false]

    entrypoint owner() : address = state.owner
    
    entrypoint timestamp() : int = Chain.timestamp

    entrypoint token() : TokenInterface = 
        switch (state.token)
            None => abort("#Token not initalized!#")
            Some(token) => token

    stateful function recursive_wallets_add(wallets: list(address)) : int =
        switch (wallets)
            wallet::wallets' =>
                put(state{ activeWallets[wallet] = true })
                1 + recursive_wallets_add(wallets')
            [] => 0

    function only_owner() =
        require(Call.origin == state.owner, "#Only Platform Manager can make this action!#")
